port module Main exposing (..)

import Browser exposing (UrlRequest)
import Browser.Navigation as Nav
import Html exposing (..)
import Html.Attributes exposing (alt, class, src)
import Html.Events exposing (onClick)
import List.Extra exposing (find, groupsOf, indexedFoldl, transpose)
import Maybe.Extra as MaybeExtra
import Url exposing (Url)


main : Program () Model Msg
main =
    Browser.application
        { init = init
        , view = view
        , update = update
        , subscriptions = subscriptions
        , onUrlRequest = UrlRequested
        , onUrlChange = UrlChanged
        }



-- PORTS


port launchConfetti : () -> Cmd msg



-- MODEL


type alias Model =
    { key : Nav.Key
    , url : Url
    , turn : Turn
    , winner : Winner
    , cells : List Cell
    , winPosition : Maybe (List ( Int, Cell ))
    }


type Turn
    = XTurn
    | OTurn


type Winner
    = Tie
    | XWins
    | OWins
    | NotFinished


type Cell
    = X
    | O
    | Empty


init : () -> Url.Url -> Nav.Key -> ( Model, Cmd Msg )
init _ url key =
    ( { key = key
      , url = url
      , turn = XTurn
      , winner = NotFinished
      , cells = List.repeat 9 Empty
      , winPosition = Nothing
      }
    , Cmd.none
    )



-- UPDATE


type Msg
    = UrlRequested UrlRequest
    | UrlChanged Url
    | Mark Int
    | Reset
    | NoOp


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        NoOp ->
            ( model, Cmd.none )

        Reset ->
            ( { model
                | turn = XTurn
                , winner = NotFinished
                , cells = List.repeat 9 Empty
                , winPosition = Nothing
              }
            , Cmd.none
            )

        UrlRequested urlRequest ->
            case urlRequest of
                Browser.Internal url ->
                    ( model, Nav.pushUrl model.key (Url.toString url) )

                Browser.External href ->
                    ( model, Nav.load href )

        UrlChanged url ->
            ( { model | url = url }
            , Cmd.none
            )

        Mark x ->
            let
                newCells =
                    markCell x model.turn model.cells

                newTurn =
                    case model.turn of
                        XTurn ->
                            OTurn

                        OTurn ->
                            XTurn

                newWinner =
                    hasGameEneded model.turn newCells

                winPos =
                    winPosition model.turn newCells
            in
            ( { model
                | cells = newCells
                , turn = newTurn
                , winner = newWinner
                , winPosition = winPos
              }
            , case newWinner of
                XWins ->
                    launchConfetti ()

                OWins ->
                    launchConfetti ()

                Tie ->
                    Cmd.none

                NotFinished ->
                    Cmd.none
            )


markCell : Int -> Turn -> List Cell -> List Cell
markCell markPos turn cells =
    cells
        |> List.indexedMap
            (\i cell ->
                if i == markPos then
                    case cell of
                        Empty ->
                            mapTurnToCell turn

                        _ ->
                            cell

                else
                    cell
            )


mapTurnToCell : Turn -> Cell
mapTurnToCell turn =
    case turn of
        XTurn ->
            X

        OTurn ->
            O


hasGameEneded : Turn -> List Cell -> Winner
hasGameEneded turn cells =
    let
        turnValue =
            mapTurnToCell turn

        rows =
            groupsOf 3 cells
                |> List.any (List.all ((==) turnValue))

        columns =
            transpose (groupsOf 3 cells)
                |> List.any (List.all ((==) turnValue))

        forwardDiagonal =
            collectValues [ 0, 4, 8 ] cells
                |> List.all ((==) turnValue)

        backwardDiagonal =
            collectValues [ 2, 4, 6 ] cells
                |> List.all ((==) turnValue)

        gameWon =
            rows || columns || forwardDiagonal || backwardDiagonal

        tie =
            List.all ((/=) Empty) cells
    in
    if gameWon then
        case turn of
            XTurn ->
                XWins

            OTurn ->
                OWins

    else if tie then
        Tie

    else
        NotFinished


winPosition : Turn -> List Cell -> Maybe (List ( Int, Cell ))
winPosition turn cells =
    let
        turnValue =
            mapTurnToCell turn

        enumeratedCells =
            List.indexedMap (\i cell -> ( i, cell )) cells

        rows =
            groupsOf 3 enumeratedCells
                |> List.map (completedToMaybe turnValue)

        columns =
            transpose (groupsOf 3 enumeratedCells)
                |> List.map (completedToMaybe turnValue)

        forwardDiagonal =
            collectValues [ 0, 4, 8 ] enumeratedCells
                |> completedToMaybe turnValue

        backwardDiagonal =
            collectValues [ 2, 4, 6 ] enumeratedCells
                |> completedToMaybe turnValue

        winPositions =
            rows ++ columns ++ [ forwardDiagonal, backwardDiagonal ]

        winPos =
            winPositions
                |> find MaybeExtra.isJust
                |> MaybeExtra.join

        tie =
            List.all ((/=) Empty) cells
    in
    if tie then
        Just enumeratedCells

    else
        winPos


completedToMaybe : Cell -> List ( Int, Cell ) -> Maybe (List ( Int, Cell ))
completedToMaybe turnValue lst =
    let
        lstCompleted =
            List.all ((==) turnValue) (List.map (\( _, cell ) -> cell) lst)
    in
    if lstCompleted then
        Just lst

    else
        Nothing


collectValues : List Int -> List a -> List a
collectValues values list =
    indexedFoldl
        (\i cur acc ->
            if List.member i values then
                cur :: acc

            else
                acc
        )
        []
        list



-- SUBSCRIPTIONS


subscriptions : Model -> Sub Msg
subscriptions _ =
    Sub.none



-- VIEW


view : Model -> Browser.Document Msg
view model =
    { title = "OXO"
    , body =
        [ section
            [ class "bg-violet-600 min-h-screen grid place-items-center" ]
            [ section
                [ class "w-96" ]
                [ header
                    [ class "w-full mx-auto mb-10" ]
                    [ img [ src "./logo.svg", class "w-fit", alt "oxo" ] [] ]
                , div
                    [ class "my-10 grid gap-4 grid-cols-3 grid-rows-3 place-items-center" ]
                    (viewBoard model)
                , case model.winPosition of
                    Just _ ->
                        div [ class "w-20 mx-auto cursor-pointer opacity-100", onClick Reset ]
                            [ img [ src "./reset.svg" ] [] ]

                    Nothing ->
                        div [ class "w-20 mx-auto opacity-0" ]
                            [ img [ src "./reset.svg" ] [] ]
                ]
            ]
        ]
    }


viewBoard : Model -> List (Html Msg)
viewBoard model =
    model.cells
        |> List.indexedMap
            (\i cell ->
                let
                    isWinPos =
                        model.winPosition
                            |> Maybe.map (List.member ( i, cell ))
                            |> Maybe.withDefault False
                in
                ( i, cell, isWinPos )
            )
        |> List.map
            (\( i, cell, isWinPos ) ->
                let
                    baseClassNames =
                        case model.winner of
                            NotFinished ->
                                "cursor-pointer border-2 border-violet-500 w-28 h-28"

                            _ ->
                                "border-2 border-violet-500 w-28 h-28"

                    classNames =
                        if isWinPos then
                            "shadow-2xl drop-shadow-2xl " ++ baseClassNames

                        else
                            baseClassNames

                    clickMsg =
                        case model.winner of
                            NotFinished ->
                                Mark i

                            _ ->
                                NoOp
                in
                case cell of
                    Empty ->
                        div
                            [ class classNames, onClick clickMsg ]
                            [ img [ src "./empty.svg" ] [] ]

                    X ->
                        div
                            [ class classNames ]
                            [ img [ src "./x.svg" ] [] ]

                    O ->
                        div
                            [ class classNames ]
                            [ img [ src "./o.svg" ] [] ]
            )
